#
# Clever programming language
# Copyright (c) 2011-2012 Clever Team
#
# CMakeLists.modopt - Module options and other stuff
#

# Modules
# ---------------------------------------------------------------------------
clever_add_module(std_regex      ON  "enable the regex module"      "")
clever_add_module(std_ffi        ON  "enable the ffi module"        "")
clever_add_module(web_cgi        ON  "enable the cgi module"        "")

# Simple modules
clever_add_simple_module(std_file       ON  "enable the file module"       "")
clever_add_simple_module(std_io         ON  "enable the io module"         "")
clever_add_simple_module(std_math       ON  "enable the math module"       "")
clever_add_simple_module(std_net        ON  "enable the net module"        "")
clever_add_simple_module(std_sys        ON  "enable the sys module"        "")
clever_add_simple_module(std_reflection ON  "enable the reflection module" "")

# std.regex
if (MOD_STD_REGEX)
	if (PCRECPP_FOUND)
		add_definitions(-DHAVE_MOD_STD_REGEX)
		list(APPEND CLEVER_INCLUDE_DIRS ${PCRECPP_INCLUDE_DIRS})
		list(APPEND CLEVER_LIBRARIES ${PCRECPP_LIBRARIES})
	else (PCRECPP_FOUND)
		clever_module_msg(std_regex "libpcrecpp not found. disabling.")
		set(MOD_STD_REGEX OFF)
	endif (PCRECPP_FOUND)
endif (MOD_STD_REGEX)

clever_module_msg(std_regex ${MOD_STD_REGEX})

# std.ffi
if (MOD_STD_FFI)
	if (MSVC)
		clever_module_msg(std_ffi "not supported under msvc. disabling.")
		set(MOD_STD_FFI OFF)
	else (MSVC)
		pkg_check_modules(FFI libffi QUIET)

		if (FFI_FOUND)
			add_definitions(-DHAVE_FFI)
			add_definitions(-DHAVE_MOD_STD_FFI)
			list(APPEND CLEVER_INCLUDE_DIRS ${FFI_INCLUDE_DIRS})
			list(APPEND CLEVER_LIBRARIES ${FFI_LIBRARIES} dl)
		else (FFI_FOUND)
			clever_module_msg(std_ffi "libffi not found. disabling.")
			set(MOD_STD_FFI OFF)
		endif (FFI_FOUND)
	endif (MSVC)
endif (MOD_STD_FFI)

clever_module_msg(std_ffi ${MOD_STD_FFI})

# web.cgi
if (MOD_WEB_CGI)
	find_library(CGICC_LIBRARIES cgicc)

	if (NOT CGICC_LIBRARIES MATCHES "NOTFOUND")
		set(CGICC_FOUND ON)
	else (NOT CGICC_LIBRARIES MATCHES "NOTFOUND")
		set(CGICC_FOUND OFF)
	endif (NOT CGICC_LIBRARIES MATCHES "NOTFOUND")

	if (CGICC_FOUND)
		string(REGEX REPLACE "lib([\\/])libcgicc.so" "include\\1cgicc\\1" CGICC_INCLUDE_DIRS ${CGICC_LIBRARIES})
		add_definitions(-DHAVE_MOD_WEB_CGI)
		list(APPEND CLEVER_INCLUDE_DIRS ${CGICC_INCLUDE_DIRS})
		list(APPEND CLEVER_LIBRARIES ${CGICC_LIBRARIES})
	else (CGICC_FOUND)
		clever_module_msg(web_cgi "libcgicc not found. disabling.")
		set(MOD_WEB_CGI OFF)
	endif (CGICC_FOUND)
endif (MOD_WEB_CGI)

clever_module_msg(web_cgi ${MOD_WEB_CGI})

